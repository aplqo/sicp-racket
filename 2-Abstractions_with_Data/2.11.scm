(load "./2.7.scm")

(define (mul-interval a b)
  (define low lower-bound)
  (define up upper-bound)
  (define (all-positive? a) (positive? (low a)))
  (define (all-negative? a) (negative? (up a)))
  (define (mul la lb ua ub)
    (make-interval (* (la a) (lb b))
                   (* (ua a) (ub b))))
  (cond ((all-negative? a)
         (cond ((all-negative? b) (mul up up low low))
               ((all-positive? b) (mul low up up low))
               (else (mul low up low low))))
        ((all-positive? a)
         (cond ((all-negative? b) (mul up low low up))
               ((all-positive? b) (mul low low up up))
               (else (mul up low up up))))
        (else
          (cond ((all-negative? b) (mul up low low low))
                ((all-positive? b) (mul low up up up))
                (else (make-interval
                        (min (* (low a) (up b)) (* (up a) (low b)))
                        (max (* (low a) (low b)) (* (up a) (up b)))))))))
